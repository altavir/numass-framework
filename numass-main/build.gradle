plugins {
    id 'groovy'
    id 'application'
    id "org.openjfx.javafxplugin"
}

javafx {
    modules = ["javafx.controls", "javafx.web"]
    version = "11"
}

apply plugin: 'kotlin'

mainClassName = 'inr.numass.LaunchGrindShell'

description = "Main numass project"

compileGroovy.dependsOn(compileKotlin)
compileGroovy.classpath += files(compileKotlin.destinationDir)

dependencies {
    api group: 'commons-cli', name: 'commons-cli', version: '1.+'
    api group: 'commons-io', name: 'commons-io', version: '2.+'
    api project(':numass-core')
    api project(':numass-core:numass-signal-processing')
    compileOnly "org.jetbrains.kotlin:kotlin-main-kts:1.3.21"
    api project(':dataforge-stat:dataforge-minuit')
    api project(':grind:grind-terminal')
    api project(":dataforge-gui")
    //api "hep.dataforge:dataforge-html"

    // https://mvnrepository.com/artifact/org.ehcache/ehcache
    //api group: 'org.ehcache', name: 'ehcache', version: '3.4.0'

}

task repl(dependsOn: classes, type: JavaExec) {
    group "numass"
    main 'inr.numass.LaunchGrindShell'
    classpath = sourceSets.main.runtimeClasspath
    description "Start Grind repl"
    standardInput = System.in
    standardOutput = System.out
    if (project.hasProperty("cmd")) {
        args = cmd.split().toList()
    }
}

task grindTask(dependsOn: classes, type: JavaExec) {
    group "numass"
    main 'inr.numass.RunTask'
    classpath = sourceSets.main.runtimeClasspath
    description "Run a task in a numass workspace"
    standardInput = System.in
    standardOutput = System.out
}

task simulate(dependsOn: classes, type: JavaExec) {
    group "numass"
    main 'inr.numass.scripts.Simulate'
    classpath = sourceSets.main.runtimeClasspath
    description "Simulate spectrum"
}

task underflow(dependsOn: classes, type: JavaExec) {
    group "numass"
    main 'inr.numass.scripts.underflow.Underflow'
    classpath = sourceSets.main.runtimeClasspath
}

task scanTreeStartScript(type: CreateStartScripts, dependsOn: installDist) {
    applicationName = 'scanTree'
    classpath = fileTree('build/install/numass-main/lib')
    mainClassName = 'inr.numass.scripts.ScanTreeKt'
    outputDir = file('build/install/numass-main/bin')
}