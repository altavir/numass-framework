buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
    }
}

apply plugin: 'com.google.protobuf'

description = "A bse package with minimal dependencies for numass"


dependencies {
    compile "hep.dataforge:dataforge-storage2"
    compile "hep.dataforge:dataforge-json"
    compile 'com.google.protobuf:protobuf-java:3.5.0'

    // https://mvnrepository.com/artifact/com.github.robtimus/sftp-fs
    compile group: 'com.github.robtimus', name: 'sftp-fs', version: '1.1.3'
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.5.0'
    }
    generatedFilesBaseDir = "$projectDir/gen"
}

compileKotlin {
    dependsOn(':numass-core:generateProto')
}

sourceSets {
    main.kotlin.srcDirs += 'gen/main/java'
}

clean {
    delete protobuf.generatedFilesBaseDir
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java");
        sourceDirs += file("src/main/proto")
    }
}